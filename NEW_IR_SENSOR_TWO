int motorspeedL = 100;  // Adjust Left Motor Speed 
int motorspeedR = 100; // Adjust Right Motor Speed

// --- Pin Definitions --- //
// IR Sensors
const byte IR_Sensor_Left = A5;
const byte IR_Sensor_Right = A4;

// Right Motor
const byte ENA = 11; // RM_Speed (PWM)
const byte IN1 = 13; // RM_FW
const byte IN2 = 12; // RM_BW

// Left Motor
const byte ENB = 3;  // LM_Speed (PWM)
const byte IN3 = A1; // LM_FW
const byte IN4 = A2; // LM_BW

// --- Memory --- //
// 0 = Straight, 1 = Left, 2 = Right
int lastTurn = 0; 

void setup() {
  pinMode(ENA, OUTPUT);
  pinMode(IN1, OUTPUT);
  pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT);
  pinMode(IN4, OUTPUT);
  pinMode(ENB, OUTPUT);
  pinMode(IR_Sensor_Left, INPUT);
  pinMode(IR_Sensor_Right, INPUT);
  rotatemotor(0, 0);
}

void loop() {
  int L = digitalRead(IR_Sensor_Left);
  int R = digitalRead(IR_Sensor_Right);

  // Case 1: (H, H) - Both on White
  if (L == HIGH && R == HIGH) {
    if (lastTurn == 1) { // Overshot a left 90-degree turn
      rotatemotor(-150, 255); // Spin left (LM_BW, RM_FW)
    }
    else if (lastTurn == 2) { // Overshot a right 90-degree turn
      rotatemotor(255, -150); // Spin right (LM_FW, RM_BW)
    }
    else { // lastTurn == 0, assume straight
      rotatemotor(motorspeedL, motorspeedR);
      lastTurn = 0;
    }
  }

  // Case 2: (H, L) - Drifting left, turn Right
  else if (L == HIGH && R == LOW) {
    rotatemotor(255, 200);
    lastTurn = 2;
  }

  // Case 3: (L, H) - Drifting right, turn Left
  else if (L == LOW && R == HIGH) {
    rotatemotor(200, 255);
    lastTurn = 1;
  }

  // Case 4: (L, L) - Both on Black (Stop)
  else {
    rotatemotor(0, 0);
    lastTurn = 0; // Reset
    delay(100);
    while (1);
  }
}

void rotatemotor(int LeftMotorSpeed, int RightMotorSpeed) {
  // --- Left Motor --- //
  if (LeftMotorSpeed < 0) { // Backward
    digitalWrite(IN3, LOW);
    digitalWrite(IN4, HIGH); // LM_BW
  }
  else if (LeftMotorSpeed > 0) { // Forward
    digitalWrite(IN3, HIGH); // LM_FW
    digitalWrite(IN4, LOW);
  }
  else { // Stop
    digitalWrite(IN3, LOW);
    digitalWrite(IN4, LOW);
  }

  // --- Right Motor --- //
  if (RightMotorSpeed < 0) { // Backward
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, HIGH); // RM_BW
  }
  else if (RightMotorSpeed > 0) { // Forward
    digitalWrite(IN1, HIGH); // RM_FW
    digitalWrite(IN2, LOW);
  }
  else { // Stop
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, LOW);
  }

  // --- Speed --- //
  // Constraining to the 0-255 PWM range
  analogWrite(ENB, constrain(abs(LeftMotorSpeed), 0, 255));
  analogWrite(ENA, constrain(abs(RightMotorSpeed), 0, 255));
}
